/*
 *  Licensed to the Apache Software Foundation (ASF) under one
 *  or more contributor license agreements.  See the NOTICE file
 *  distributed with this work for additional information
 *  regarding copyright ownership.  The ASF licenses this file
 *  to you under the Apache License, Version 2.0 (the
 *  "License"); you may not use this file except in compliance
 *  with the License.  You may obtain a copy of the License at
 *
 *    https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing,
 *  software distributed under the License is distributed on an
 *  "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 *  KIND, either express or implied.  See the License for the
 *  specific language governing permissions and limitations
 *  under the License.
 */

import org.gradle.api.plugins.jvm.JvmTestSuite

/*
 *  Licensed to the Apache Software Foundation (ASF) under one
 *  or more contributor license agreements.  See the NOTICE file
 *  distributed with this work for additional information
 *  regarding copyright ownership.  The ASF licenses this file
 *  to you under the Apache License, Version 2.0 (the
 *  "License"); you may not use this file except in compliance
 *  with the License.  You may obtain a copy of the License at
 *
 *    https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing,
 *  software distributed under the License is distributed on an
 *  "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 *  KIND, either express or implied.  See the License for the
 *  specific language governing permissions and limitations
 *  under the License.
 */

testing {
    suites {
        test {
            useSpock('2.3-groovy-3.0')
        }

        functionalTest(JvmTestSuite) {
            useSpock('2.3-groovy-3.0')

            dependencies {
                implementation project()
                implementation gradleTestKit()
                implementation 'commons-io:commons-io:2.16.1'
                implementation 'org.junit.platform:junit-platform-launcher'
            }

            targets {
                all {
                    testTask.configure { shouldRunAfter(test) }
                }
            }
        }
    }
}

tasks.named('functionalTest').configure { Test it ->
    def skipTests = project.hasProperty('skipTests')
    it.onlyIf {
        !skipTests
    }

    it.systemProperty('localMavenPath', rootProject.layout.buildDirectory.dir('local-maven').get().asFile.absolutePath)
    it.systemProperty('grailsGradlePluginVersion', project.version)
    it.systemProperty('groovyVersion', functionTestGroovyVersion)

    it.inputs.dir(project.layout.projectDirectory.dir('src/functionalTest/resources/publish-projects'))
            .withPathSensitivity(PathSensitivity.RELATIVE)
            .withPropertyName('example-gradle-projects')

    it.testLogging {
        exceptionFormat = 'full'
        events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
    }

    it.dependsOn 'publishAllPublicationsToTestCaseMavenRepoRepository'
}

gradlePlugin.testSourceSets.add(sourceSets.functionalTest)

tasks.named('check') {
    dependsOn(testing.suites.functionalTest)
}
